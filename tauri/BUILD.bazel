load("@rules_rust//cargo:defs.bzl", "cargo_build_script")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")
load("//tools:defs.bzl", "expand_template")

filegroup(
    name = "desktop_file",
    srcs = [
        "moosync.desktop",
    ],
)

filegroup(
    name = "resources",
    srcs = glob([
        "icons/**/*",
        "capabilities/**/*",
    ]),
)

expand_template(
    name = "tauri_config",
    data = [
        "//ui:moosync_ui",
    ],
    out_name = "tauri.conf.json",
    substitutions = {
        # Relative from tauri build script but we need to escape to exec root
        "@DIST@": "../../../../$(execpath //ui:moosync_ui)",
        "@DESKTOP@": "moosync.desktop",
        "@ICONS@": "icons",
    },
    template = "tauri.conf.json",
)

rust_library(
    name = "app_lib",
    srcs = [
        "src/db/mod.rs",
        "src/extensions/mod.rs",
        "src/extensions/request_handler.rs",
        "src/lib.rs",
        "src/librespot/mod.rs",
        "src/logger/mod.rs",
        "src/lyrics/mod.rs",
        "src/macros.rs",
        "src/mobile_player/mod.rs",
        "src/mpris/mod.rs",
        "src/oauth/handler.rs",
        "src/oauth/mod.rs",
        "src/preference_holder/mod.rs",
        "src/providers/common.rs",
        "src/providers/extension.rs",
        "src/providers/handler.rs",
        "src/providers/mod.rs",
        "src/providers/spotify.rs",
        "src/rodio/mod.rs",
        "src/scanner/mod.rs",
        "src/themes/mod.rs",
        "src/updater/desktop.rs",
        "src/updater/mobile.rs",
        "src/updater/mod.rs",
        "src/window/handler.rs",
        "src/window/mod.rs",
    ],
    compile_data = [
        ":resources",
        "//ui:moosync_ui",
    ],
    data = [
        ":tauri_config",
    ],
    proc_macro_deps = [
        "@crates//:async-trait",
        "//ui/tauri-invoke-proc:tauri_invoke_proc",
    ],
    rustc_env = {
        "TAURI_CONF_JSON": "$(execpath :tauri_config)",
    },
    visibility = ["//visibility:public"],
    deps = [
        ":build_script",
        "//core/database",
        "//core/extensions",
        "//core/file_scanner",
        "//core/librespot",
        "//core/lyrics",
        "//core/mpris",
        "//core/preferences",
        "//core/rodio_player",
        "//core/themes",
        "//core/types",
        "//lib/tauri-plugin-audioplayer",
        "//lib/tauri-plugin-file-scanner",
        "//lib/tauri-plugin-self-update",
        "@crates//:chrono",
        "@crates//:clap",
        "@crates//:futures",
        "@crates//:oauth2",
        "@crates//:regex",
        "@crates//:reqwest",
        "@crates//:rspotify",
        "@crates//:rustls",
        "@crates//:serde",
        "@crates//:serde_json",
        "@crates//:tauri",
        "@crates//:tauri-plugin-autostart",
        "@crates//:tauri-plugin-deep-link",
        "@crates//:tauri-plugin-dialog",
        "@crates//:tauri-plugin-opener",
        "@crates//:tauri-plugin-single-instance",
        "@crates//:tauri-plugin-updater",
        "@crates//:tauri-utils",
        "@crates//:tokio",
        "@crates//:tracing",
        "@crates//:tracing-appender",
        "@crates//:tracing-subscriber",
        "@crates//:url",
    ] + select({
        "@platforms//os:linux": [
            "@crates//:webkit2gtk",
        ],
        "//conditions:default": [],
    }),
)

rust_binary(
    name = "moosync",
    srcs = ["src/main.rs"],
    visibility = ["//visibility:public"],
    deps = [
        ":app_lib",
        ":build_script",
        "@crates//:tracing",
    ],
)

cargo_build_script(
    name = "build_script",
    srcs = ["build.rs"],
    build_script_env = {
        "DEP_TAURI_DEV": "false",
    },
    crate_root = "build.rs",
    data = [
        "Cargo.toml",
        ":tauri_config",
    ] + glob(["src/**/*"]),
    visibility = ["//visibility:public"],
    deps = [
        "@crates//:tauri-build",
    ],
)
